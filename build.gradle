group 'xyz.ronella.tools'
version '1.0.0-SNAPSHOT'

apply plugin: 'groovy'

sourceCompatibility = 1.8

ext {
    packDirPath = "${buildDir}/pack"
    buildBaseName = 'sql-servant'
}

repositories {
    mavenCentral()
}

configurations {
    drivers
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.11'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.0'
    drivers group: 'com.h2database', name: 'h2', version: '1.4.197'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

jar {
    it.baseName = buildBaseName

    manifest {
        attributes "Main-Class": "sqlsrvnt"
    }
}

task pack(type: Zip, dependsOn: 'jar') {
    it.baseName = buildBaseName
    destinationDir = file(packDirPath)

    from(file("${sourceSets.main.resources.srcDirs.first()}/conf")) {
        into 'conf'
        include '*.json'
    }
    from(sourceSets.main.resources.srcDirs.first()) {
        include '*.bat'
    }
    from(configurations.compile) {
        into 'libs'
    }
    from(configurations.drivers) {
        into 'drivers'
    }
    from(file("${buildDir}/libs")) {
        include 'sql*.jar'
        into 'libs'
    }
}