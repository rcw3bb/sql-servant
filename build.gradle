import org.apache.tools.ant.filters.*

group 'xyz.ronella.tools'
version '1.2.0-SNAPSHOT'

apply plugin: 'groovy'

sourceCompatibility = 1.8

ext {
    packDirPath = "${buildDir}/pack"
    buildBaseName = 'sql-servant'
}

repositories {
    mavenCentral()
}

configurations {
    drivers
    mssql
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.11'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.0'
    drivers group: 'com.h2database', name: 'h2', version: '1.4.197'
    mssql group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '7.0.0.jre8'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

jar {
    it.baseName = buildBaseName
    exclude 'conf'
    exclude 'drivers'
    exclude '*.bat'
}

processResources {
    from (sourceSets.main.resources) {
        include '**/*.properties'
        filter ReplaceTokens, tokens : ['app.version': project.version,
                                        'year' : new Date().format('MMM YYYY')]
    }
}

String getLibs() {
    configurations.compile.inject(new String(), {___aggr, ___file ->
        "${___aggr}${(___aggr.length()>1 ? ';' : '')}%LIBS_DIR%\\${___file.name}"})
}

String getDrvs() {
    configurations.drivers.inject(new String(), {___aggr, ___file ->
        "${___aggr}${(___aggr.length()>1 ? ';' : '')}%DRVS_DIR%\\${___file.name}"})
}

String getDrvsMSSQL() {
    configurations.mssql.inject(getDrvs(), {___aggr, ___file ->
        "${___aggr}${(___aggr.length()>1 ? ';' : '')}%DRVS_DIR%\\${___file.name}"})
}

task pack(type: Zip, dependsOn: 'jar') {
    it.baseName = "${buildBaseName}-noarch"
    destinationDir = file(packDirPath)

    from(file("${sourceSets.main.resources.srcDirs.first()}/conf")) {
        into 'conf'
        include 'ss-h2*.json'
    }
    from(sourceSets.main.resources.srcDirs.first()) {
        include '*.bat'
        filter ReplaceTokens, tokens : ['app.version': project.version,
                                        'libraries' : getLibs(),
                                        'drivers' : getDrvs(),
                                        'java.library.path' : '']
    }
    from(configurations.compile) {
        into 'libs'
    }
    from(configurations.drivers) {
        into 'drivers'
    }
    from(file("${buildDir}/libs")) {
        include 'sql*.jar'
        into 'libs'
    }
}

task packMSSQLx64(type: Zip, dependsOn: 'jar') {
    it.baseName = "${buildBaseName}-win64"
    destinationDir = file(packDirPath)

    from(file("${sourceSets.main.resources.srcDirs.first()}/conf")) {
        into 'conf'
        include 'ss-*.json'
    }
    from(sourceSets.main.resources.srcDirs.first()) {
        include '*.bat'
        filter ReplaceTokens, tokens : ['app.version': project.version,
                                        'libraries' : getLibs(),
                                        'drivers' : getDrvsMSSQL(),
                                        'java.library.path' : '-Djava.library.path=%DRVS_DIR%']
    }
    from(configurations.compile) {
        into 'libs'
    }
    from(configurations.drivers) {
        into 'drivers'
    }
    from(configurations.mssql) {
        into 'drivers'
    }
    from(file("${sourceSets.main.resources.srcDirs.first()}/drivers/sqljdbc_6.0/x64")) {
        include 'sql*.dll'
        into 'drivers'
    }
    from(file("${buildDir}/libs")) {
        include 'sql*.jar'
        into 'libs'
    }
}

task packMSSQLx86(type: Zip, dependsOn: 'jar') {
    it.baseName = "${buildBaseName}-win32"
    destinationDir = file(packDirPath)

    from(file("${sourceSets.main.resources.srcDirs.first()}/conf")) {
        into 'conf'
        include 'ss-*.json'
    }
    from(sourceSets.main.resources.srcDirs.first()) {
        include '*.bat'
        filter ReplaceTokens, tokens : ['app.version': project.version,
                                        'libraries' : getLibs(),
                                        'drivers' : getDrvsMSSQL(),
                                        'java.library.path' : '-Djava.library.path=%DRVS_DIR%']
    }
    from(configurations.compile) {
        into 'libs'
    }
    from(configurations.drivers) {
        into 'drivers'
    }
    from(configurations.mssql) {
        into 'drivers'
    }
    from(file("${sourceSets.main.resources.srcDirs.first()}/drivers/sqljdbc_6.0/x86")) {
        include 'sql*.dll'
        into 'drivers'
    }
    from(file("${buildDir}/libs")) {
        include 'sql*.jar'
        into 'libs'
    }
}

task packAll(dependsOn: ['pack', 'packMSSQLx64', 'packMSSQLx86'])